#!/usr/bin/perl

use Moo;                 # {{{1
use strictures 2;
use 5.006;
use 5.038_001;
use version; our $VERSION = qv('0.3');
use namespace::clean;    # }}}1

{

  package Dn::Internal;

  use Moo;               # {{{1
  use strictures 2;
  use namespace::clean;
  use Clipboard;
  use Const::Fast;
  use MooX::HandlesVia;
  use MooX::Options (
    authors      => 'David Nebauer <david [at] nebauer [dot] org>',
    description  => 'Run passfzf in an alacritty terminal',
    protect_argv => 0,
  );
  use Path::Tiny;
  use Types::Standard;

  const my $TRUE       => 1;
  const my $CLIP_DELAY => 45;    # }}}1

  # attributes

  # _capture    {{{1
  has '_capture' => (
    is      => 'rw',
    isa     => Types::Standard::InstanceOf ['Path::Tiny'],
    lazy    => $TRUE,
    default => sub {
      return Path::Tiny->tempfile({ realpath => $TRUE });
    },
    doc => 'Capture filepath',
  );    # }}}1

  # methods

  # main()    {{{1
  #
  # does:   main method
  # params: nil
  # prints: feedback
  # return: n/a, dies on failure
  sub main ($self) {

    # call passfzf
    my @cmd;
    push @cmd, 'alacritty';
    push @cmd, '--class',   'PassFzf_Capture';
    push @cmd, '--title',   'PassFzf_Operations';
    push @cmd, '--command', 'passfzf', '--capture', $self->_capture;
    system @cmd;

    # check for clipping
    my $capture_file = $self->_capture;
    if (not $capture_file->is_file) {
      die "Cannot locate capture file '$capture_file'\n";
    }
    if ($capture_file->slurp) {

      # send clipping to all X selections
      Clipboard->copy_to_all_selections($capture_file->slurp);

      # wait 45 seconds
      sleep $CLIP_DELAY;

      # clear all X selections
      Clipboard->copy_to_all_selections(q{});
    }

    # clean up
    $capture_file->remove;

    return;
  }    # }}}1

}

my $p = Dn::Internal->new_with_options->main;

1;

# POD    {{{1
__END__

=encoding utf8

=head1 NAME

passfzf-capture - wrapper for passfzf

=head1 USAGE

B<passfzf-capture>

B<passfzf-capture -h>

=head1 DESCRIPTION

A wrapper for B<passfzf>. The script B<passfzf> is a console utility that uses
B<fzf> to select a B<pass> name and choose what password-related operation to
perform. Some of those operations result in a password being sent to the X
clipboard selection.

This script is designed to address a problem with B<passfzf>: if a shell
command invokes a new terminal, B<alacritty> in the test case, to run
B<passfzf>, the 'clipped' passwords do not appear in the system's X clipboard
selection.

This script runs B<passfzf> the same way, but invokes it in such a way that any
clipped password is saved to a specified disk file. This script then harvests
the clipped password and sends it to the system clipboard.

=head2 Different clipping behaviour

The B<pass> utility is well-behaved with respect to X selections. It sends the
clipped password to the X clipboard selection and, after 45 seconds, restores
the X clipboard selection to its previous state.

This script is not nearly so well-behaved. It sends the clipped password to all
3 X selections: primary, secondary and clipboard. After 45 seconds it clears
all 3 X selections.

The reason for the different behaviour is due to limitations in B<xclip> (tried
and abandoned as part of the solution) and the L<Clipboard> module.

=head1 CONFIGURATION

This script does not use configuration files or environmental variables.

=head1 REQUIRED ARGUMENTS

None.

=head1 OPTIONS

=over

=item B<-h>  B<--help>

Display help and exit.

=back

=head1 EXIT STATUS

The exit code is 0 for successful execution and 1 if the script does a
controlled exit following an error. If the script crashes unexpectedly the
error code is that given by the system.

=head1 DIAGNOSTICS

See B<passfzf>, B<pass> and B<fzf> for the errors and warnings those apps
emit.

=head1 INCOMPATIBILITIES

There are no known incompatibilities.

=head1 BUGS AND LIMITATIONS

Please report any bugs to the author.

=head1 DEPENDENCIES

=head2 Perl modules

Clipboard, Const::Fast, Moo, MooX::HandlesVia, MooX::Options, namespace::clean,
Path::Tiny, strictures, Types::Standard, version.

=head2 Executables

alacritty, fzf, pass, passfzf.

=head1 AUTHOR

David Nebauer L<mailto:david[at]nebauer[dot]org>

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2025 L<David Nebauer|mailto:david[at]nebauer[dot]org>

This script adopts the licensing of B<passfzf> chosen by its original author,
L<Mathieu Laparie|mailto:mlaparie[at]disr[dot]it>:

MIT License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

=cut

# vim:foldmethod=marker:
